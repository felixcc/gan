allprojects {
    apply plugin: 'idea'
}

subprojects {
    group = 'com.github.safrain'
    version = '1.0'
    apply plugin: 'java'
    apply plugin: 'maven'

    configurations {
        provided
    }

    idea.module {
        scopes.PROVIDED.plus += configurations.provided
    }

    sourceSets {
        main { compileClasspath += configurations.provided }
    }

    repositories {
        mavenCentral()
    }
}

project(':gadmin-lib') {
    dependencies {
        provided 'javax.servlet:servlet-api:2.5'
        provided 'org.springframework:spring:2.5.6.SEC03'
        provided 'org.codehaus.groovy:groovy-all:1.8.7'
    }
}

project(':gadmin-example') {
    apply plugin: 'war'
    apply plugin: 'jetty'
    [jettyRun, jettyRunWar].each { it.contextPath = '/' }
    dependencies {
        compile 'javax.servlet:servlet-api:2.5'
        compile 'org.springframework:spring:2.5.6.SEC03'
        compile 'org.codehaus.groovy:groovy-all:1.8.7'
        compile project(':gadmin-lib')
    }
}

