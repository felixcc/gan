allprojects {
    apply plugin: 'idea'
    apply plugin: 'maven'
    group = 'com.github.safrain'
    version = '1.0-SNAPSHOT'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'

    configurations {
        provided
    }

    idea.module {
        scopes.PROVIDED.plus += configurations.provided
    }

    sourceSets {
        main { compileClasspath += configurations.provided }
    }
}

project(':gan') {
    dependencies {
        provided 'javax.servlet:servlet-api:2.5'
        provided 'org.springframework:spring:2.5.6.SEC03'
        provided 'org.codehaus.groovy:groovy-all:1.8.7'
    }
    uploadArchives {
        repositories {
            mavenDeployer {
                repository(url: 'file://localhost/tmp/myRepo/')
                pom.project {
                    description ''
                    url  'safrain.github.io/gan/'
                    licenses {
                        license {
                            name 'WTFPL v2'
                            url 'http://www.wtfpl.net'
                            distribution 'repo'
                        }
                    }
                    scm {
                        url  'http://safrain.github.io/gan/'
                        connection  'scm:git:git@github.com:safrain/gan.git'
                        developerConnection  'scm:git:git@github.com:safrain/gan.git'
                    }
                    developers {
                        developer {
                            id  'safrain'
                            name  'safrain'
                            email  'z.safrain@gmail.com'
                        }
                    }
                }
            }
        }
    }
}
project(':gan-example') {
    apply plugin: 'war'
    apply plugin: 'jetty'
    [jettyRun, jettyRunWar].each {
        it.contextPath = '/'
    }
    dependencies {
        compile 'javax.servlet:servlet-api:2.5'
        compile 'org.springframework:spring:2.5.6.SEC03'
        compile 'org.codehaus.groovy:groovy-all:1.8.7'
        compile project(':gan')
    }
}