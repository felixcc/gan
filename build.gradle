allprojects {
    apply plugin: 'idea'
    apply plugin: 'maven'
    apply plugin: 'signing'
    group = 'com.github.safrain'
    version = '1.0'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'

    configurations {
        provided
    }

    idea.module {
        scopes.PROVIDED.plus += configurations.provided
    }

    sourceSets {
        main { compileClasspath += configurations.provided }
    }
}

project(':gan') {
    dependencies {
        provided 'javax.servlet:servlet-api:2.5'
        provided 'org.springframework:spring:2.5.6.SEC03'
        provided 'org.codehaus.groovy:groovy-all:1.8.7'
    }



    signing {
        sign configurations.archives
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from 'build/docs/javadoc'
    }

    task sourcesJar(type: Jar) {
        from sourceSets.main.allSource
        classifier = 'sources'
    }

    artifacts {
        archives jar
        archives javadocJar
        archives sourcesJar
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { signing.signPom(it) }

                repository(url:
                        version.endsWith('SNAPSHOT') ?
                            'https://oss.sonatype.org/content/repositories/snapshots/' : 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
                ) {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                pom.project {
                    name 'GrooveAdmiN'
                    description 'GrooveAdmiN is a light weight debugging/management tool embedded in Java web applications.'
                    url 'http://safrain.github.io/gan/'
                    licenses {
                        license {
                            name 'WTFPL v2'
                            url 'http://www.wtfpl.net'
                            distribution 'repo'
                        }
                    }
                    scm {
                        url 'http://safrain.github.io/gan/'
                        connection 'scm:git:git@github.com:safrain/gan.git'
                        developerConnection 'scm:git:git@github.com:safrain/gan.git'
                    }
                    developers {
                        developer {
                            id 'safrain'
                            name 'safrain'
                            email 'z.safrain@gmail.com'
                        }
                    }
                }
            }
        }
    }
}
project(':gan-example') {
    apply plugin: 'war'
    apply plugin: 'jetty'
    [jettyRun, jettyRunWar].each {
        it.contextPath = '/'
    }
    dependencies {
        compile 'javax.servlet:servlet-api:2.5'
        compile 'org.springframework:spring:2.5.6.SEC03'
        compile 'org.codehaus.groovy:groovy-all:1.8.7'
        compile project(':gan')
    }
}